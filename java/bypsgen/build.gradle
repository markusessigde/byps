import org.gradle.plugins.ide.eclipse.model.AccessRule

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
version = versionFromProperties

eclipse {
  classpath {
    file {
      whenMerged {
        def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
        jre.accessRules.add(new AccessRule('0', 'jdk/javadoc/**'))
      }
    }
  }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'res'
        }
    }
}


jar {
    archiveName = "bypsgen.jar"

    manifest {
        attributes 'Implementation-Title': 'BYPS generatore',
                   'Implementation-Version': version,
                   'Main-Class': 'byps.gen.Main'
    }
 
    from {
        configurations.compileOnly.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
}

dependencies {
	compileOnly project(":byps-api")
	compileOnly project(":byps-override")
	compileOnly project(":byps")
	
	// https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
	compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.1'
	
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

uploadArchives {
  repositories {
    mavenDeployer {

      // put the credentials into ~/.gradle/gradle.properties
      def mavenUrl = version.endsWith("SNAPSHOT") ? mavenUploadUrlSnapshots : mavenUploadUrl
      repository(url: mavenUrl) {
        authentication(userName: mavenUser, password: mavenPassword)
      }
      pom.version = version
      pom.groupId = "byps"
      pom.artifactId = "bypsgen"
      pom.packaging = "jar"
    }
  }
}
